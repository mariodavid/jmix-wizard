buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.jmix.gradle:jmix-gradle-plugin:1.2.0"
    }
}

group = 'de.diedavids.jmix.wizard'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url 'https://global.repo.jmix.io/repository/public'
        }
    }

    jmix {
        bomVersion = '1.2.1'
        projectId = 'wizard'
    }

    group = this.group
    version = this.version

    if (it.name != "jmix-wizard-example") {
        def props = new Properties()
        buildFile.withInputStream { props.load(it) }
        def subArchivesBaseName = props.getProperty('archivesBaseName')
        def archName = subArchivesBaseName.substring(1, subArchivesBaseName.length() - 1)


        sourceCompatibility = '1.8'

        java {
            withSourcesJar()
        }

        artifacts {
            archives sourcesJar
        }

        publishing {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
                    credentials {
                        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                        password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                    }
                }
            }
            publications {
                javaMaven(MavenPublication) {
                    artifactId = "jmix-wizard" //archName
                    from components.java
                }
            }
        }

        // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
        tasks.withType(GenerateModuleMetadata) {
            enabled = false
        }
    }

}
